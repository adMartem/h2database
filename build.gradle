/**
 * NOTE: H2 can now build itself using the gradle java build. It is imported in the p3cobol parent project
 * and shadowed when building the p3cobol-library.
 * This project builds a full distribution jar of H2 that is compatible with the p3cobol shadowed
 * portion used for the indexed file storage manager.  This must be used if an H2 database is to
 * be accessed by both the indexed file system and as a SQL database.
 */

plugins {
    id 'java'
    id 'eclipse'
}

dependencies {
    implementation('javax.servlet:javax.servlet-api:3.1.0') { transitive = false }
    implementation('org.apache.lucene:lucene-core:3.6.2') { transitive = false }
    implementation('org.slf4j:slf4j-api:1.6.0')  { transitive = false }
    compileOnly('org.osgi:org.osgi.core:4.2.0') { transitive = false }
    compileOnly('org.osgi:org.osgi.enterprise:4.2.0') { transitive = false }
    implementation('org.locationtech.jts:jts-core:1.16.0') { transitive = false }

    testImplementation fileTree(dir: 'h2/ext')
    testImplementation group: 'junit', name: 'junit', version: '4.+'
}

sourceSets {
    main {
        java {
            srcDirs 'h2/src/main'
        }
        resources {
            srcDirs 'h2/src/main'
        }
    }
    test {
        java {
            srcDirs 'h2/src/test'
            srcDirs 'h2/src/tools/org/h2/dev'
            srcDirs 'h2/src/tools/org/h2/mode'
            srcDirs 'h2/src/tools/org/h2/jaqu'
        }
    }
}

eclipse {
    classpath {
        sourceSets = [ sourceSets.main ]
    }
}

jar {
    manifest {
        attributes provider: 'Gradle Build',
                'Implementation-Title': 'H2 (P3/COBOL) Database System',
                'Implementation-Version': project.version,
                'Main-Class': 'org.h2.tools.Console'
    }
}
