/**
 * NOTE: H2 can now build itself using the gradle java build. It is imported in the p3cobol parent project
 * and shadowed when building the p3cobol-library.
 * This project builds a full distribution jar of H2 that is compatible with the p3cobol shadowed
 * portion used for the indexed file storage manager.  This must be used if an H2 database is to
 * be accessed by both the indexed file system and as a SQL database.
 */

plugins {
    id 'java'
    id 'eclipse'
    id "org.gradlex.extra-java-module-info" version "1.3"
}
// add module information for all direct and transitive dependencies that are not modules
extraJavaModuleInfo {

    failOnMissingModuleInfo.set(false)    
    
    knownModule("org.slf4j:slf4j-api", "org.slf4j.api")
    
    automaticModule("javax.servlet:javax.servlet-api", "p3cobol.javax.servlet.api")
    automaticModule("org.apache.lucene:lucene-core", "p3cobol.lucene.core")
    automaticModule("org.osgi:org.osgi.core", "p3cobol.osgi.core")
    automaticModule("org.osgi:org.osgi.enterprise", "p3cobol.osgi.enterprise")
    automaticModule("org.locationtech.jts:jts-core", "p3cobol.jts.core")

    /*
    module("commons-cli:commons-cli", "org.apache.commons.cli") {
        exports("org.apache.commons.cli")
    }
    module("commons-collections:commons-collections", "org.apache.commons.collections")
    automaticModule("commons-logging:commons-logging", "org.apache.commons.logging")
    */
}

dependencies { 

    /** Sl4j */
    implementation('org.slf4j:slf4j-api:2.0.7') { transitive = false }
    
    implementation('org.apache.lucene:lucene-core:3.6.2') { transitive = false }
    implementation('javax.servlet:javax.servlet-api:3.1.0') { transitive = false }
    compileOnly('org.osgi:org.osgi.core:4.2.0') { transitive = false }
    compileOnly('org.osgi:org.osgi.enterprise:4.2.0') { transitive = false }
    implementation('org.locationtech.jts:jts-core:1.16.0') { transitive = false }

    testImplementation fileTree(dir: 'h2/ext')
}

sourceSets {
    main {
        java {
            srcDirs 'h2/src/main'
        }
        resources {
            srcDirs 'h2/src/main'
        }
    }
    test {
        java {
            srcDirs 'h2/src/test'
            srcDirs 'h2/src/tools/org/h2/dev'
            srcDirs 'h2/src/tools/org/h2/mode'
            srcDirs 'h2/src/tools/org/h2/jaqu'
        }
    }
}

compileJava {
    options.compilerArgs << '-XDignore.symbol.file' // remove warnings for deprecated and unsafe packages/methods
    options.fork = true
    //options.forkOptions.executable = 'javac'
    options.release = 17                            // compile for object version 17 regardless of the Gradle JVM (only Java 10+)
}

eclipse {
    classpath {
        sourceSets = [ sourceSets.main ]
    }
}

jar {
    manifest {
        attributes provider: 'Gradle Build',
                'Implementation-Title': 'H2 (P3/COBOL) Database System',
                'Implementation-Version': project.version,
                'Main-Class': 'org.h2.tools.Console'
    }
}
